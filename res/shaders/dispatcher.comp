#version 460
#extension GL_EXT_nonuniform_qualifier : require
// size of a workgroup for compute
layout(local_size_x = 256) in;

struct Instance {
    mat4 transform;
    vec4 sphere;
    uint firstIndex;
    uint indexCount;
    int vertexOffset;
    uint material;
};

struct IndexedIndirectCommand {
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    int vertexOffset;
    uint firstInstance;
};

struct DrawParam {
    mat4 transform;
    uint material;
    uint pad[3];
};
layout(binding = 1, std430) writeonly buffer DrawParams { DrawParam params[]; }
drawParams[];

layout(push_constant) uniform constants {
    vec4 left, right, front, back, top, bottom;
    uint indirectDrawBind;
    uint multiDrawDataBind;
    uint instanceBind;
    uint drawParamsBind;
};

layout(binding = 1, std430) writeonly buffer IndirectDraws {
    IndexedIndirectCommand commands[];
}
indirectDraw[];

layout(binding = 1, std430) readonly buffer Instances { Instance instances[]; }
instances[];

layout(binding = 1, std430) buffer MultiDrawData {
    uint drawCounts;
    uint maxDraws;
}
multiDrawData[];

bool isVisible(uint instance) {
    vec4 center =
        vec4(instances[instanceBind].instances[instance].sphere.xyz, 1);
    float l = instances[instanceBind].instances[instance].sphere.w;
    return (dot(left, center)) > -l && (dot(right, center) > -l) &&
           (dot(top, center)) > -l && (dot(bottom, center) > -l) &&
           (dot(front, center)) > -l && (dot(back, center) > -l);
}

void main() {
    uint gID = gl_GlobalInvocationID.x;

    if (gID < multiDrawData[multiDrawDataBind].maxDraws && isVisible(gID)) {
        uint index = atomicAdd(multiDrawData[multiDrawDataBind].drawCounts, 1);

        indirectDraw[indirectDrawBind].commands[index].indexCount =
            instances[instanceBind].instances[gID].indexCount;

        indirectDraw[indirectDrawBind].commands[index].instanceCount = 1;

        indirectDraw[indirectDrawBind].commands[index].firstIndex =
            instances[instanceBind].instances[gID].firstIndex;

        indirectDraw[indirectDrawBind].commands[index].vertexOffset =
            instances[instanceBind].instances[gID].vertexOffset;

        indirectDraw[indirectDrawBind].commands[index].firstInstance = index;

        drawParams[drawParamsBind].params[index].transform =
            instances[instanceBind].instances[gID].transform;

        drawParams[drawParamsBind].params[index].material =
            instances[instanceBind].instances[gID].material;
    }
}